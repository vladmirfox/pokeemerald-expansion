const LOCALID_BIGSIS = 1
const LOCALID_RIVALS_NOTE = 2

mapscripts SunriseVillage_PlayersHouse_LR_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		// Put rival's note on the table
		if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) < 4) {
			setmetatile(4, 4, METATILE_HouseBeige_Scroll, TRUE)
		} else {
			removeobject(LOCALID_RIVALS_NOTE)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SUNRISE_VILLAGE_INTRO_STATE, 2: SunriseVillage_PlayersHouse_LR_EventScript_BigSisSaysHi
    ]	
}

script SunriseVillage_PlayersHouse_LR_EventScript_BigSisSaysHi {
    lock
	playse(SE_PIN)
	applymovement(LOCALID_BIGSIS, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_BIGSIS, Common_Movement_Delay48)
	waitmovement(0)
	turnobject(LOCALID_BIGSIS, DIR_EAST)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)

	special(GetPlayerBrotherSisterString)
	speakername("Big sis")
	msgbox(format(
		"Oh, {PLAYER}, you're up! Ready for the big event?\p"
		"Are you excited? I sure hope you are, because I'm VERY EXCITED!\p"
		"I have to head out! Don't be late, I know you, little {STR_VAR_1}!\p"
		"By the way, your friend Natsuki left you a note.\p"
		"Make sure you read it!"
	))
	closemessage
	applymovement(LOCALID_BIGSIS, SunriseVillage_PlayersHouse_LR_Movement_BigSisExits)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	waitmovement(0)
	playse(SE_EXIT)
	waitse
	removeobject(LOCALID_BIGSIS)
	
	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 3)
	setflag(FLAG_HIDE_BIGSIS_HOME)

    release
    end
}

movement SunriseVillage_PlayersHouse_LR_Movement_BigSisExits {
	walk_down * 2
	walk_right
	walk_down * 2
	step_end
}

script SunriseVillage_PlayersHouse_LR_EventScript_BigSis {
	lock
	faceplayer
	speakername("Big sis")
	if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) <= 4) {
		msgbox(format(
			"What are you still doing here? You're late for the distribution!\p"
			"Go to Kaba's house right now!"
		))
		closemessage
	} elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) <= 6) {
		special(GetPlayerBrotherSisterString)
		msgbox(format(
			"Hey little {STR_VAR_1}! See how cute my new Pokémon is?\p"
			"By the way, I heard Natsuki was waiting for you at Elder Kaba's house. You should probably go see them."
		))
		closemessage
	} elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) == 7) {
		special(GetPlayerBrotherSisterString)
		bufferspeciesname(STR_VAR_2, VAR_STARTER_MON)
		msgbox(format(
			"Hey little {STR_VAR_1}! See how cute my new Pokémon is?\p"
			"Oh, who is that lovely little thing with you?\p"
			"{STR_VAR_2}, huh?\p"
			"Looks like it already likes you a lot! You're going to make a great wielder, I know it!\p"
			"… … … … … …\n… … … … … …\p"
			"So you're going on a journey…\p"
			"That's great! I'm proud of you, {PLAYER}.\p"
			"Take some of these Pokéballs for your trip, in case you find any good Pokémon!"
		))
		closemessage

		giveitem(ITEM_POKE_BALL, 5)

		speakername("Big sis")
		msgbox(format(
			"Oh, and I'm sure you're going to get lost, so I'll lend you my map of Toku!"
		))
		closemessage

		giveitem(ITEM_TOWN_MAP)

		special(GetPlayerBrotherSisterString)
		speakername("Big sis")
		msgbox(format(
			"Alrighty! You all set? Have a safe journey, little {STR_VAR_1}.\p"
			"Come say hi sometimes though, will you?"
		))
		closemessage

		setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 8)

		// Default
	} else {
		special(GetPlayerBroSisString)
		msgbox(format(
			"Hey {PLAYER}! How's it going?\p"
			"Here, rest for a bit."
		))
		closemessage
		call(Common_EventScript_OutOfCenterPartyHeal)
		speakername("Big sis")
		msgbox(format(
			"See you around, little {STR_VAR_1}."
		))
		closemessage
	}
	release
}

script SunriseVillage_PlayersHouse_LR_EventScript_Eevee {
	lock
	faceplayer
	waitse
	playmoncry(SPECIES_EEVEE, CRY_MODE_NORMAL)
	ismonseen(SPECIES_EEVEE)
    if (var(VAR_RESULT)) {
        speakername("Eevee")
    } else {
        speakername("Fluffy fox")
    }
	msgbox(format("Eee!"))
	waitmoncry
	release
}

script SunriseVillage_PlayersHouse_LR_EventScript_RivalsNote {
    lock
	msgbox(format(
		"“Hey {PLAYER},\n"
		"I'm going down to the beach before the giveaway, meet me there if you wanna hang out.\p"
		"-Natsuki”"
	))
	closemessage
    release
    end
}
