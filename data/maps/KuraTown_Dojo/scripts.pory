mapscripts KuraTown_Dojo_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		// All the trees are cut from the start if the player has already beaten the dojo
		if (flag(FLAG_BADGE02_GET)) {
			setflag(FLAG_TEMP_1)
			setflag(FLAG_TEMP_2)
			setflag(FLAG_TEMP_3)
			setflag(FLAG_TEMP_5)
			setflag(FLAG_TEMP_6)
			setflag(FLAG_TEMP_8)
			setflag(FLAG_TEMP_9)
		}
	}
}

script KuraTown_Dojo_EventScript_Takao {
	trainerbattle_single(
		TRAINER_TAKAO,
		format("Ho ho ho… It seems you have taken a wrong turn…"),
		format("Very impressive skills!")
	)
	msgbox(
		format("Not all paths lead to the master."),
		MSGBOX_AUTOCLOSE
	)
	end
}

script KuraTown_Dojo_EventScript_Jocelyn {
	trainerbattle_single(
		TRAINER_JOCELYN,
		format("Is the maze giving you trouble?"),
		format("At least I didn't give you any trouble…")
	)
	msgbox(format(
		"You're wondering why you can't just cut down all the trees?\p"
		"Uhhh… It's a secret!"
	), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Laura {
	trainerbattle_single(
		TRAINER_LAURA,
		format("No! Don't cut down that tree! It's my favorite tree!"),
		format("Sniffle…")
	)
	msgbox(format("It's okay, go ahead. They're all my favorite tree anyway."), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Brenden {
	trainerbattle_single(
		TRAINER_BRENDEN,
		format("Take a deep breath! The air is so rich and flowery here!"),
		format("A-tchoo!")
	)
	msgbox(format("Sniff… By zeazonal allergiez are agdig up agaid…"), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Cristian {
	trainerbattle_single(
		TRAINER_CRISTIAN,
		format("Did you know any tree can be bonsaified by pruning its leaves regularly?"),
		format("You're not here because you're particularly interested in bonsai, are you?")
	)
	msgbox(format("I feel bad sometimes, not letting the tree grow the way it wants."), MSGBOX_AUTOCLOSE)
	end
}

script KuraTown_Dojo_EventScript_Lilith {
	trainerbattle_single(
		TRAINER_LILITH,
		format("Hana may look frail, but she's actually way tougher than you!"),
		format("Stop it! I'm a delicate flower!")
	)
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(format(
			"I saw your fight peeking through the leaves! That was amazing!\p"
			"I'm officially a fan!"
		))
		closemessage
		addvar(VAR_PLAYER_REPUTATION, 1)
	} else {
		msgbox(format("I wish I could see you two fight! Good luck!"), MSGBOX_AUTOCLOSE)
	}
	end
}

script KuraTown_Dojo_EventScript_Hana {
	trainerbattle_single(
		TRAINER_HANA_1,
		// TODO different dialogue if you already met
		format(
			"Welcome to my humble Dojo. I am Hana. Did you enjoy my little maze?\p"
			"Tending to my bonsai and fighting alongside my Pokémon are my favorite pastimes. With this Dojo, I can do both!\p"
			"A Pokémon battle can be graceful like a budding flower, strong like the deepest roots, powerful like a windswept pine tree, all at once.\p"
			"I want you to show me all the grace, strength and power you are capable of!"
		),
		format("Amazing."),
		KuraTown_Dojo_EventScript_HanaDefeated,
		NO_MUSIC
	)
	// specialvar(VAR_RESULT, ShouldTryRematchBattle)
	// goto_if_eq(VAR_RESULT, TRUE, KuraTown_Dojo_EventScript_HanaRematch)

	msgbox(
		format(
			"The slow-growing tree becomes the sturdiest. Take your time on your journey, {PLAYER}."
		),
		MSGBOX_DEFAULT
	)

	release
	end
}

script KuraTown_Dojo_EventScript_HanaDefeated {
	message(format(
		"{PLAYER} received the Carved Token\n"
		"from Hana."
	))
	waitmessage
	call(Common_EventScript_PlayGymBadgeFanfare)
	msgbox(
		format(
			// TODO EVA boost atk?
			"Thanks to this Token, your Pokémon will obey you without question, even those you trade with others.\p"
			"If you are interested in submitting to the Pokémon Council challenge, you will need to collect a Token from each of the eight Dojos scattered throughout the region.\p"
			"It is not an easy task, but I can feel determination taking root in your heart.\p"
			"Here, let me also give you this."
		),
		MSGBOX_DEFAULT
	)
	setflag(FLAG_BADGE02_GET)
	setvar(VAR_0x8008, 2)
	call(Common_EventScript_SetGymTrainers)
	call(KuraTown_Dojo_EventScript_GiveBulldoze)
	closemessage
	// TODO rematch
	// delay(30)
	// playfanfare(MUS_REGISTER_MATCH_CALL)
	// msgbox(format("Registered Hana\nin the address log."), MSGBOX_DEFAULT)
	// waitfanfare
	// closemessage
	release
	end
}

script KuraTown_Dojo_EventScript_GiveBulldoze {
	giveitem(ITEM_TM_BULLDOZE)
	goto_if_eq(VAR_RESULT, 0, Common_EventScript_BagIsFull)
	msgbox(
		format(
			"This scroll contains Bulldoze, a powerful ground-type move that slows down the enemy.\p"
			"I invented that technique myself! I needed a way to soften the ground when repotting bonsai, heh heh.\p"
		),
		MSGBOX_DEFAULT
	)
	return
}

script KuraTown_Dojo_EventScript_Advice {
	lock
	faceplayer
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(format(
			"That was a beautiful fight! You're going to blossom into an excellent Pokémon wielder, I can just feel it!"
		))
	} else {
		if (flag(FLAG_RECEIVED_HM_CUT) && flag(FLAG_BADGE01_GET)) {
			msgbox(format(
				"Hello, young challenger, and welcome to the official Dojo of Kura Village!\p"
				"Lady Hana, the Dojo master, is an expert in all things bonsai. She designed this place and grew these trees, of course!\p"
				"She tends to favor ground-type moves, but she's got other tricks up her sleeve, too. Be on your guard!\p"
				"Now, pay attention: only some of these pretty trees can be cut down.\p"
				"Figure out which, and you'll reach the master!"
			))
		} else {
			if (flag(FLAG_BADGE01_GET)) {
				msgbox(format(
					"Hello, young challenger, and welcome to the official Dojo of Kura Village!\p"
					"All these trees are blocking your way, aren't they?\p"
					"You should come back once you've figured out a way to cut them!"
				))
			} else {
				msgbox(format(
					"Hello, young challenger, and welcome to the official Dojo of Kura Village!\p"
					"All these trees are blocking your way, aren't they?\p"
					"You should come back once you've figured out a way to cut them!\p"
					"If you're itching for a challenge, why not try your skills at the Dojo in Maguro Harbor first?\p"
					"It's not very far east from here!"
				))
			}
		}
	}
	release
}

script KuraTown_Dojo_EventScript_DojoStatue {
	lockall
	if (flag(FLAG_BADGE02_GET)) {
		msgbox(
			"Dojo of Kura Village\p"
			"Certified wielders:\n"
			"Natsuki,"
			"{PLAYER}",
			MSGBOX_SIGN
		)
	} else {
		msgbox(
			"Dojo of Kura Village\p"
			"Certified wielders:\n"
			"Natsuki",
			MSGBOX_SIGN
		)
	}
	releaseall
}

script KuraTown_Dojo_EventScript_KuraDojoTree_CanCut {
	lock
	if (flag(FLAG_RECEIVED_HM_CUT) && flag(FLAG_BADGE01_GET)) {
		msgbox("…Oh? This tree can be cut down!", MSGBOX_DEFAULT)
		closemessage
		playse(SE_M_CUT)
		applymovement(VAR_LAST_TALKED, Movement_CutTreeDown)
		waitse
		waitmovement(0)
		removeobject(VAR_LAST_TALKED)
	} else {
		call(KuraTown_Dojo_EventScript_KuraDojoTree_CantCut)
	}
	release
}

script KuraTown_Dojo_EventScript_KuraDojoTree_CantCut {
	msgbox("A gorgeous and well-nurtured tree.", MSGBOX_SIGN)
}