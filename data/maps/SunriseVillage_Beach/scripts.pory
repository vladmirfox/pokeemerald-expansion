mapscripts SunriseVillage_Beach_MapScripts {
}

const LOCALID_RIVAL = 1

script SunriseVillage_Beach_EventScript_RivalSpeech {
    lock
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	faceplayer
	speakername("Natsuki")
	msgbox(format(
		"Hey, {PLAYER}! Come on, sit down next to me."
	))
	closemessage

	// Player sits next to rival
	switch(var(VAR_FACING)) {
		case DIR_SOUTH:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromTop)
		case DIR_NORTH:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromBottom)
		case DIR_WEST:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromRight)
		case DIR_EAST:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromLeft)
	}
	waitmovement(0)
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSitsOnBench)
	waitmovement(0)

	speakername("Natsuki")
	msgbox(format(
		"It's been crazy lately, huh?\p"
		"With the Pokémon in Ginko Woods,\nand the volcano…\p"
		"It feels like everything's\nabout to change.\p"
		"Everyone is so anxious.\p"
		"… … … … … …\n… … … … … …"
	))

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Are you worried?",
		"What are you thinking about?"
	)
	closemessage


	switch(var(VAR_RESULT)) {
		case 0:
			speakername("Natsuki")
			msgbox(format(
				"Nah, you know me.\nWe'll figure it out when we get there.\p"
				"That's what I've always done,\neverything turns out fine eventually."
			))
			closemessage

			applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
			waitmovement(0)

			speakername("Natsuki")
			msgbox(format(
				"Hey, look.\p"
				"You can see Yifu City very clearly\ntoday, it's gorgeous!\p"
				"…"
			))
			closemessage

			delay(48)
			// They face each other
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

			speakername("Natsuki")
			msgbox(format(
				"You okay there, {PLAYER}?\nYou look troubled.\p"
				"Aren't you excited to become\na Pokémon wielder?"
			))
		case 1:
			speakername("Natsuki")
			msgbox(format(
				"Huh?…\p"
				"…Oh, sorry, I was just wondering\nwhich Pokémon I'm gonna be getting."
			))
			closemessage

			applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
			waitmovement(0)

			speakername("Natsuki")
			msgbox(format(
				"Hey, look.\p"
				"You can see Yifu City very clearly\ntoday, it's gorgeous!\p"
				"…"
			))
			closemessage

			delay(48)
			// They face each other
			applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

			speakername("Natsuki")
			msgbox(format(
				"What about you, {PLAYER}?\nYou excited about getting\lyour own Pokémon?"
			))
	}

	dynmultichoice(
		0,
		0,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"I'm thrilled!",
		"I'm terrified!",
		"It's whatever."
	)
	closemessage

	speakername("Natsuki")
	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"Hell yeah!\p"
				"We're going to make awesome wielders,\nI can already tell.\p"
				"Maybe you'll even be second best\none day…\p"
				"Behind me, of course!"
			))
		case 1:
			msgbox(format(
				"Uh, you're kidding, right?\p"
				"It's going to be awesome.\nJust chill.\p"
				"Once we get our Pokémon, we'll do\nwhatever we want!"
			))
		case 2:
			msgbox(format(
				"Oh c'mon, you're just pretending\nnot to be excited.\p"
				"Look at you, acting all tough\nand silent…\lBut I see right through you!\p"
				"I bet you'll be a great wielder.\nMaybe even as good as me…\p"
				"Probably not though!"
			))
	}
	closemessage

	delay(48)

	// Volcano quake!
	setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 20)  // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
	special(ShakeCamera)
	delay(16)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSurprisedByQuake)
	delay(16)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_PlayerSurprisedByQuake)
	waitstate
	waitmovement(OBJ_EVENT_ID_PLAYER)
	waitmovement(LOCALID_RIVAL)
	delay(48)

	// They face each other
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	speakername("Natsuki")
	msgbox(format(
		"Woah, there it goes again!\n"
		"You good, {PLAYER}?\p"
		"Whew! Those quakes are no joke, huh?\p"
		"Maybe the volcano's telling us\nto get to Elder Kaba's house already."
	))
	closemessage

	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)

	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSeesDistribution)
	waitmovement(0)

	speakername("Natsuki")
	msgbox(format(
		"Oh Shuckles, the distribution's\nalready started!\p"
		"Let's go, {PLAYER}!!"
	))
	closemessage

	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	// Rival runs towards the village
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalRaces)
	waitmovement(0)

	setflag(FLAG_HIDE_SUNRISE_BEACH_RIVAL)
	clearflag(FLAG_HIDE_SUNRISE_VILLAGE_STANDARD_NPC_POSITIONS)
	clearflag(FLAG_HIDE_BIGSIS_HOME)
	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 4)

	removeobject(LOCALID_RIVAL)
    release
    end
}

movement SunriseVillage_Beach_Movement_PlayerSurprisedByQuake {
	emote_exclamation_mark
	delay_16
	face_right
	delay_16
	face_up
	delay_16
	face_right
	delay_16
	face_down
	delay_16
	face_left
	delay_16
	face_up
}

movement SunriseVillage_Beach_Movement_RivalSurprisedByQuake {
	emote_exclamation_mark
	delay_16
	face_down
	delay_16
	face_up
	delay_16
	face_left
	delay_16
	face_right
	delay_16
	face_up
	delay_16
	face_down
}

// Corsola
script SunriseVillage_Beach_EventScript_Corsola {
	lock
    faceplayer
    waitse
    playmoncry(SPECIES_CORSOLA, CRY_MODE_NORMAL)
    msgbox(format(
        "Krr!"
    ))
    waitmoncry
    release
    end
}

script SunriseVillage_Beach_EventScript_Corsola2 {
	lock
    faceplayer
    waitse
    playmoncry(SPECIES_CORSOLA, CRY_MODE_WEAK)
    msgbox(format(
        "Laaa!"
    ))
    waitmoncry
    release
    end
}

movement SunriseVillage_Beach_Movement_RivalSitsOnBench {
	walk_in_place_faster_left
	delay_16 * 5
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromLeft {
	walk_up
	walk_right
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromTop {
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromRight {
	walk_up
	walk_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromBottom {
	walk_right
	walk_up * 2
	walk_left
	step_end
}

movement SunriseVillage_Beach_Movement_RivalSeesDistribution {
	face_right
	delay_16
	delay_16
	delay_16
	face_player
	step_end
}

movement SunriseVillage_Beach_Movement_RivalRaces {
	walk_fast_right * 9
	step_end
}