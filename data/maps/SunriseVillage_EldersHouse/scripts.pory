const LOCALID_ELDER = 1
const LOCALID_BALL_ARON = 3
const LOCALID_BALL_SPHEAL = 4
const LOCALID_BALL_TRAPINCH = 5
const LOCALID_ARON = 6
const LOCALID_SPHEAL = 7
const LOCALID_TRAPINCH = 8
const LOCALID_RIVAL = 2

mapscripts SunriseVillage_EldersHouse_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SUNRISE_VILLAGE_INTRO_STATE, 4: SunriseVillage_EldersHouse_EventScript_DistributionCutscene
    ]
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_TEMP_0, 0 {
			// Hiding the mons inside their ball
			removeobject(LOCALID_ARON)
			removeobject(LOCALID_SPHEAL)
			removeobject(LOCALID_TRAPINCH)
			clearflag(FLAG_RECEIVED_STARTER)
		}
	]
}

script SunriseVillage_EldersHouse_EventScript_DistributionCutscene {
	lock
	// Walk up to elder
	applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerWalkUpToElder)
	waitmovement(0)
	applymovement(LOCALID_ELDER, Common_Movement_WalkInPlaceFasterDown)

	// Elder speaks
	speakername("Elder Kaba")
	msgbox((
		"{PLAYER}, Natsuki.\nYou are late. Why am I surprised?\p"
		"I always have to run after you two!\nNow stay still for a minute and\llisten closely!\p"
		"As you have heard, the woods, and even\nthe roads, are no longer safe.\p"
		"We don't yet know why, but the wild\nPokémon that usually hide in the\ltall grass and live peacefully\lalongside men have started behaving\laggressively.\p"
		"There are reports of travelers being\nattacked from several nearby villages.\p"
		"This is all very worrying…$"
	))
	closemessage
	applymovement(LOCALID_ELDER, SunriseVillage_EldersHouse_Movement_ElderTurnsToThink)
	waitmovement(0)
	speakername("Elder Kaba")
	msgbox(format(
		"Which is why!\p"
		"The Toku council has decided that,\nfor the time being, the best way to\lhandle this is to use the Pokémon\lthemselves as a means of protection!\p"
		"There were very few experienced\nPokémon wielders in Toku until now.\l"
		"This is about  change.\p"
		"With a trained Pokémon by your side,\nyou'll be able to defend yourselves\lon your own.\p"
		"Without further ado, please carefully\nchoose your new companion!"
	))
	closemessage

	applymovement(LOCALID_ELDER, SunriseVillage_EldersHouse_Movement_ElderOpensFirstBall)
	waitmovement(0)
	playse(SE_BALL_OPEN)
	waitse
	removeobject(LOCALID_BALL_ARON)
	addobject(LOCALID_ARON)
	playmoncry(SPECIES_ARON, CRY_MODE_NORMAL)
	delay(30)

	applymovement(LOCALID_ELDER, SunriseVillage_EldersHouse_Movement_ElderOpensSecondBall)
	waitmovement(0)
	playse(SE_BALL_OPEN)
	waitse
	removeobject(LOCALID_BALL_SPHEAL)
	addobject(LOCALID_SPHEAL)
	playmoncry(SPECIES_SPHEAL, CRY_MODE_NORMAL)
	delay(30)

	applymovement(LOCALID_ELDER, SunriseVillage_EldersHouse_Movement_ElderOpensThirdBall)
	waitmovement(0)
	playse(SE_BALL_OPEN)
	waitse
	removeobject(LOCALID_BALL_TRAPINCH)
	addobject(LOCALID_TRAPINCH)
	playmoncry(SPECIES_TRAPINCH, CRY_MODE_NORMAL)
	delay(30)

	clearflag(FLAG_RECEIVED_STARTER)

	applymovement(LOCALID_ELDER, SunriseVillage_EldersHouse_Movement_ElderReturnsToInitialPos)
	waitmovement(0)

	playse(SE_LEDGE)
	applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalIsDeslighted)
	waitmovement(0)
	speakername("Natsuki")
	msgbox(format("Woah! Cool."))

	// Rival is nice and lets you pick first
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	speakername("Natsuki")
	msgbox(format(
		"{PLAYER}, choose yours first!\n"
		"I don't mind being second pick."
	))
	closemessage
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)

	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 5)
	release
}

movement SunriseVillage_EldersHouse_Movement_ElderOpensFirstBall {
	walk_right
	face_up
	delay_16
}

movement SunriseVillage_EldersHouse_Movement_ElderOpensSecondBall {
	walk_right
	face_up
	delay_16
}

movement SunriseVillage_EldersHouse_Movement_ElderOpensThirdBall {
	walk_right
	face_up
	delay_16
}

movement SunriseVillage_EldersHouse_Movement_ElderReturnsToInitialPos {
	walk_left * 3
	face_down
}

movement SunriseVillage_EldersHouse_Movement_RivalIsDeslighted {
	jump_in_place_up
}

script SunriseVillage_EldersHouse_Event_Rival {
	lock
	faceplayer
	speakername("Natsuki")
	if (flag(FLAG_RECEIVED_STARTER)) {
		msgbox(format(
			"You alright?\p"
			"C'mon, let's fight again, and give it your all this time!"
		))
		closemessage
		// Battle!
		goto(SunriseVillage_EldersHouse_EventScript_RivalBattle)
	} else {
		msgbox(format("Don't ask me to pick for you!"))
		closemessage
		applymovement(LOCALID_RIVAL, Common_Movement_FaceUp)
		waitmovement(0)
	}
	release
}

script SunriseVillage_EldersHouse_Event_Elder {
	lock
	faceplayer
	speakername("Elder Kaba")
	if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 7) {
		// TODO EVA dex evaluation, letter delivery, etc
		msgbox(format("How is your Pokémon adventure going?"))
		closemessage
	} else {
		// Player has yet to pick a Pokémon
		if (!flag(FLAG_RECEIVED_STARTER)) {
			msgbox(format("Choose your new partner wisely.\nNo takebacks."))
			closemessage
			// Player has lost their first battle and has to do it again
		} else {
			msgbox(format("You're free to battle here, but please don't break anything…"))
			closemessage
		}
	}
	release
}

script SunriseVillage_EldersHouse_EventScript_TryToLeave {
	lock
	turnobject(LOCALID_ELDER, DIR_SOUTH)
	speakername("Elder Kaba")
	msgbox(format(
		"Where do you think you're going?"
	))
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement(0)
	release
}

script SunriseVillage_EldersHouse_EventScript_TryToEnterOtherRoom {
	lock
	turnobject(LOCALID_ELDER, DIR_NORTH)
	speakername("Elder Kaba")
	msgbox(format(
		"Where do you think you're going?"
	))
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	waitmovement(0)
	release
}

movement SunriseVillage_EldersHouse_Movement_PlayerGetAwayFromExit {
	walk_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerWalkUpToElder {
	walk_up * 2
	step_end
}

movement SunriseVillage_EldersHouse_Movement_ElderTurnsToThink {
	walk_in_place_faster_up
	delay_16 * 4
	walk_in_place_faster_down
	step_end
}

script SunriseVillage_EldersHouse_Event_Pokeball_Starter_Trapinch {
	lock
	setvar(VAR_TEMP_1, SPECIES_TRAPINCH)
	call(SunriseVillage_EldersHouse_Event_Common_Pokeball_Starter)
	release
}

script SunriseVillage_EldersHouse_Event_Pokeball_Starter_Aron {
	lock
	setvar(VAR_TEMP_1, SPECIES_ARON)
	call(SunriseVillage_EldersHouse_Event_Common_Pokeball_Starter)
	release
}

script SunriseVillage_EldersHouse_Event_Pokeball_Starter_Spheal {
	lock
	setvar(VAR_TEMP_1, SPECIES_SPHEAL)
	call(SunriseVillage_EldersHouse_Event_Common_Pokeball_Starter)
	release
}

script SunriseVillage_EldersHouse_Event_Common_Pokeball_Starter {
	if (flag(FLAG_RECEIVED_STARTER)) {
		msgbox(format(
			"You already got a Pokémon.\nBetter leave this one here."
		))
		closemessage
		return
	}
	playmoncry(VAR_TEMP_1, CRY_MODE_NORMAL)
	showmonpic(VAR_TEMP_1, 10, 3)
	bufferspeciesname(STR_VAR_1, VAR_TEMP_1)
	switch(var(VAR_TEMP_1)) {
		case SPECIES_SPHEAL:
			msgbox(format("Do you choose {STR_VAR_1},\nsoul of the crashing sea waves?"), MSGBOX_YESNO)
		case SPECIES_ARON:
			msgbox(format("Do you choose {STR_VAR_1},\nnoble warrior of iron?"), MSGBOX_YESNO)
		case SPECIES_TRAPINCH:
			msgbox(format("Do you choose {STR_VAR_1},\nswift dancer of sandstorms?"), MSGBOX_YESNO)
	}
	if (var(VAR_RESULT) == NO || var(VAR_RESULT) == MULTI_B_PRESSED) {
		hidemonpic
		speakername("Elder Kaba")
		msgbox(format("Take the time you need."))
		closemessage
		return
	} elif (var(VAR_RESULT) == YES) {
		switch(var(VAR_TEMP_1)) {
			case SPECIES_SPHEAL:
				givemon(SPECIES_SPHEAL, 5, ITEM_ORAN_BERRY, move1=MOVE_FLIPPER_SPLASH, move2=MOVE_TAIL_WHIP, move3=MOVE_NONE, move4=MOVE_NONE)
			case SPECIES_ARON:
				givemon(SPECIES_ARON, 5, ITEM_ORAN_BERRY, move1=MOVE_TACKLE, move2=MOVE_HARDEN, move3=MOVE_NONE, move4=MOVE_NONE)
			case SPECIES_TRAPINCH:
				givemon(SPECIES_TRAPINCH, 5, ITEM_ORAN_BERRY, move1=MOVE_MUD_SLAP, move2=MOVE_SAND_ATTACK, move3=MOVE_NONE, move4=MOVE_NONE)
		}
		playfanfare(MUS_OBTAIN_ITEM)
		message(format("{PLAYER} received the Pokémon {STR_VAR_1}!"))
		waitmessage
		waitfanfare

		switch(var(VAR_TEMP_1)) {
			case SPECIES_SPHEAL:
				setvar(VAR_STARTER_MON, SPECIES_SPHEAL)
				removeobject(LOCALID_SPHEAL)
				removeobject(LOCALID_BALL_SPHEAL)
			case SPECIES_ARON:
				removeobject(LOCALID_ARON)
				removeobject(LOCALID_BALL_ARON)
				setvar(VAR_STARTER_MON, SPECIES_ARON)
			case SPECIES_TRAPINCH:
				removeobject(LOCALID_TRAPINCH)
				removeobject(LOCALID_BALL_TRAPINCH)
				setvar(VAR_STARTER_MON, SPECIES_TRAPINCH)
		}

		msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES){
			call(Common_EventScript_GetGiftMonPartySlot)
			call(Common_EventScript_NameReceivedPartyMon)
		}
		closemessage
	}
	hidemonpic

	setflag(FLAG_RECEIVED_STARTER)
	setflag(FLAG_SYS_POKEMON_GET)

	turnobject(LOCALID_RIVAL, DIR_NORTH)

	// Put the player back in their initial spot
	switch(var(VAR_STARTER_MON)) {
		case SPECIES_SPHEAL:
			switch(var(VAR_FACING)) {
				case DIR_SOUTH:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingSphealFromTop)
				case DIR_NORTH:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingSphealFromBottom)
			}
		case SPECIES_ARON:
			switch(var(VAR_FACING)) {
				case DIR_SOUTH:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingAronFromTop)
				case DIR_NORTH:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingAronFromBottom)
				case DIR_EAST:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingAronFromLeft)
			}
		case SPECIES_TRAPINCH:
			switch(var(VAR_FACING)) {
				case DIR_SOUTH:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingTrapinchFromTop)
				case DIR_NORTH:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingTrapinchFromBottom)
				case DIR_WEST:
					applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingTrapinchFromRight)
			}
	}

	waitmovement(OBJ_EVENT_ID_PLAYER)

	turnobject(LOCALID_ELDER, DIR_SOUTH)

	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)

	// Rival walks up to his Pokémon
	speakername("Natsuki")
	msgbox(format("Alright. I'm taking this one!"))
	closemessage

	switch(var(VAR_STARTER_MON)) {
		case SPECIES_SPHEAL:
			applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalWalkUpToAron)
			waitmovement(0)
			removeobject(LOCALID_ARON)
			removeobject(LOCALID_BALL_ARON)
			applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalWalkAwayFromAron)
			waitmovement(0)
		case SPECIES_ARON:
			applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalWalkUpToTrapinch)
			waitmovement(0)
			removeobject(LOCALID_TRAPINCH)
			removeobject(LOCALID_BALL_TRAPINCH)
			applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalWalkAwayFromTrapinch)
			waitmovement(0)
		case SPECIES_TRAPINCH:
			applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalWalkUpToSpheal)
			waitmovement(0)
			removeobject(LOCALID_SPHEAL)
			removeobject(LOCALID_BALL_SPHEAL)
			applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalWalkAwayFromSpheal)
			waitmovement(0)
	}

	// Elder gives Pokédex
	speakername("Elder Kaba")
	msgbox(format(
		"Now that you have your own Pokémon,\nyou two are officially\lPokémon wielders!\p"
		"The more wielders there are,\nthe deeper our understanding\lof Pokémon will be.\p"
		"We need to study these creatures\nfrom up close to find the root of\lthis issue.\p"
		"Actually, there's something\nI wanted to ask you."
	))
	closemessage

	applymovement(LOCALID_ELDER, SunriseVillage_EldersHouse_Movement_ElderGivesDex)
	waitmovement(0)

	playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received the Pokédex!"))
	waitfanfare
	setflag(FLAG_SYS_POKEDEX_GET)
	// TODO EVA why does this exist
	setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
	closemessage

	delay(20)

	speakername("Natsuki")
	msgbox(format(
		"What's this? A book?"
	))
	closemessage

	delay(20)

	speakername("Elder Kaba")
	msgbox(format(
		"I call this the “Pokédex”.\nIt is an encyclopedia containing\lvital information about Pokémon\lfrom the whole region!\p"
		"Except it's, well, empty.\nThat's where you come in!\p"
		"Explore Toku and fill its pages with\nyour findings!\p"
		"You can start by going all the way to\nSilveridge, north of here in the\lmountains.\p"
		"Another Elder lives there and,\nhopefully, has more information\las to why the Pokémon are lashing out.\p"
		"Could you deliver him this letter?"
	))
	closemessage

	playfanfare(MUS_OBTAIN_ITEM)
	// TODO EVA giveitem letter
	message(format("{PLAYER} received the Letter!"))
	waitfanfare
	closemessage

	speakername("Natsuki")
	msgbox(format("Understood, Grandma Kaba.\nWe're going adventuring!"))
	closemessage

	applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterLeft)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	waitmovement(LOCALID_RIVAL)

	speakername("Natsuki")
	msgbox(format("{PLAYER}… Are you ready?"))
	dynmultichoice(
		16,
		6,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Totally!",
		"Absolutely not!"
	)

	if (var(VAR_RESULT) == 0) {
		msgbox(format(
				"That's what I'm talking about!"
			))
		closemessage
	} else {
		applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)

		speakername("Natsuki")
		checkplayergender
		if (var(VAR_RESULT) == MALE) {
			msgbox(format(
				"Don't listen to him! He's so ready!"
			))
		} else {
			msgbox(format(
				"Don't listen to her! She's so ready!"
			))
		}
		closemessage

		applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterLeft)
	}

	speakername("Natsuki")
	msgbox(format(
		"{PLAYER}!\nLet's see what our brand-new Pokémon\lare capable of!\p"
		"I request an official Pokémon\nwielder battle!"
	))
	closemessage

	applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalPreBattlePose)
	waitmovement(0)

	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 6)

	// Battle!
	goto(SunriseVillage_EldersHouse_EventScript_RivalBattle)
}

movement SunriseVillage_EldersHouse_Movement_RivalPreBattlePose {
	// TODO EVA
	// zubats_align
	face_player
}

script SunriseVillage_EldersHouse_EventScript_PostBattle {
	special(HealPlayerParty)
	specialvar(VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_WON) {
		speakername("Natsuki")
		msgbox(format(
			"Oh, man!\nThat was the most fun I've ever had!\p"
			"{PLAYER}!\nWe're now officially rivals!"
			"I went easy on you this time, but\nnext time… You'll see!\p"
			"I'm going home to pack some things.\p"
			"See ya later, Feraligatr."
		))
		closemessage
	} else {
		speakername("Natsuki")
		msgbox(format(
			"Oh, man!\nThat was the most fun I've ever had!\p"
			"{PLAYER}!\nWe're now officially rivals!\p"
			"You better work hard to keep up!\p"
			"I'm going home to pack some things.\p"
			"See ya later, Feraligatr."
		))
		closemessage
	}

	// No idea why VAR_FACING doesn't work here, fuck it
	// TODO EVA: specialvar(VAR_RESULT, GetPlayerFacingDirection)
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 6 && var(VAR_TEMP_1) == 7) {
		applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalLeavesWhenPlayerIsBottom)
	} else {
		applymovement(LOCALID_RIVAL, SunriseVillage_EldersHouse_Movement_RivalLeaves)
	}

	waitmovement(LOCALID_RIVAL)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	removeobject(LOCALID_RIVAL)
	playse(SE_EXIT)
	waitse
	delay(16)

	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 7)

	release
}

script SunriseVillage_EldersHouse_EventScript_RivalBattle {
	switch(var(VAR_STARTER_MON)) {
		case SPECIES_SPHEAL:
			trainerbattle_no_intro_no_whiteout(TRAINER_RIVAL_FIRST_BATTLE_SPHEAL, format("Looks like I lost!"))
		case SPECIES_ARON:
			trainerbattle_no_intro_no_whiteout(TRAINER_RIVAL_FIRST_BATTLE_ARON, format("Looks like I lost!"))
		case SPECIES_TRAPINCH:
			trainerbattle_no_intro_no_whiteout(TRAINER_RIVAL_FIRST_BATTLE_TRAPINCH, format("Looks like I lost!"))
	}

	goto(SunriseVillage_EldersHouse_EventScript_PostBattle)
}

script SunriseVillage_EldersHouse_EventScript_Joltik {
	lock
    faceplayer
    waitse
    playmoncry(SPECIES_JOLTIK, CRY_MODE_NORMAL)
    msgbox(format(
        "Fss!"
    ))
    waitmoncry
    release
    end	
}

movement SunriseVillage_EldersHouse_Movement_RivalWalkUpToSpheal {
	walk_up
	delay_16
	step_end
}

movement SunriseVillage_EldersHouse_Movement_RivalWalkAwayFromSpheal {
	walk_down
	walk_in_place_faster_up
	delay_16
	step_end
}

movement SunriseVillage_EldersHouse_Movement_RivalWalkUpToTrapinch {
	walk_up
	walk_right
	walk_in_place_faster_up
	delay_16
	step_end
}

movement SunriseVillage_EldersHouse_Movement_RivalWalkAwayFromTrapinch {
	walk_left
	walk_down
	walk_in_place_faster_up
	delay_16
	step_end
}


movement SunriseVillage_EldersHouse_Movement_RivalWalkUpToAron {
	walk_up
	walk_left
	walk_in_place_faster_up
	delay_16
	step_end
}

movement SunriseVillage_EldersHouse_Movement_RivalWalkAwayFromAron {
	walk_right
	walk_down
	walk_in_place_faster_up
	delay_16
	step_end
}

movement SunriseVillage_EldersHouse_Movement_ElderGivesDex {
	walk_right * 2
	walk_in_place_faster_down
	delay_16
	walk_left
	walk_in_place_faster_down
	delay_16
	step_end
}

movement SunriseVillage_EldersHouse_Movement_RivalLeaves {
	walk_down * 2
	step_end
}


movement SunriseVillage_EldersHouse_Movement_RivalLeavesWhenPlayerIsBottom {
	walk_left
	walk_down * 2
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingSphealFromTop {
	walk_down * 2
	walk_left
	walk_down
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingSphealFromBottom {
	walk_left
	walk_down
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingAronFromTop {
	walk_down * 3
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingAronFromBottom {
	walk_down
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingAronFromLeft {
	walk_left
	walk_down * 2
	walk_right * 2
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingTrapinchFromTop {
	walk_right
	walk_down * 2
	walk_left * 2
	walk_down
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingTrapinchFromBottom {
	walk_left * 2
	walk_down
	walk_in_place_faster_up
	step_end
}

movement SunriseVillage_EldersHouse_Movement_PlayerGoesBackAfterPickingTrapinchFromRight {
	walk_down
	walk_left * 2
	walk_down
	walk_in_place_faster_up
	step_end
}