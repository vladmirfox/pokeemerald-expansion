const FLAG_HIDE_HOOTHOOT = FLAG_TEMP_1
const LOCALID_PSYDUCK = 3
const LOCALID_HOOTHOOT = 4

mapscripts SakuTown_House3_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: SakuTown_House3_EventScript_SetUpPokemon
}

script SakuTown_House3_EventScript_SetUpPokemon {
    if (!flag(FLAG_FORTREE_NPC_TRADE_COMPLETED)) {
        setflag(FLAG_HIDE_HOOTHOOT)
    }
}

script SakuTown_House3_EventScript_Psyduck {
    lock
    faceplayer
    speakername("Psyduck")
    msgbox(format(
        "Psaïaïaï…"
    ))
    playmoncry(SPECIES_PSYDUCK, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script SakuTown_House3_EventScript_Hoothoot {
    lock
    faceplayer
    speakername("Hoothoot")
    msgbox(format(
        "Hooo…"
    ))
    playmoncry(SPECIES_HOOTHOOT, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

script SakuTown_House3_EventScript_TradeMe {
    lock
	faceplayer
    if (flag(FLAG_FORTREE_NPC_TRADE_COMPLETED)) {
        speakername("Little boy")
        msgbox(format("Thank you!"))
        closemessage
    } else {
        setvar(VAR_0x8008, INGAME_TRADE_PSYDUCK)
        copyvar(VAR_0x8004, VAR_0x8008)
        specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
        copyvar(VAR_0x8009, VAR_RESULT)

        speakername("Little boy")
        msgbox(
            format(
                "The best thing about Pokémon is trading them around!\p"
                "I want a Hoothoot, but my pa doesn't let me go out at night.\p"
                "I'll trade you a super dope Psyduck for it!"
            ), MSGBOX_YESNO
        )

	    if (var(VAR_RESULT) == NO || var(VAR_RESULT) == MULTI_B_PRESSED) {
            speakername("Little boy")
            msgbox(format("Keep your Hoothoot, I don't want it anyway…"))
            closemessage
        } else {
	        special(ChoosePartyMon)
            waitstate
            copyvar(VAR_0x800A, VAR_0x8004)
            if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
                speakername("Little boy")
                msgbox(format("Keep your Hoothoot, I don't want it anyway…"))
                closemessage
            } else {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                if (var(VAR_RESULT) != VAR_0x8009) {
                    speakername("Little boy")
                    msgbox(format("I don't want your weird Pokémon! I want a Hoothoot!"))
                    closemessage
                } else {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)

                    setflag(FLAG_FORTREE_NPC_TRADE_COMPLETED)
                    clearflag(FLAG_HIDE_HOOTHOOT)
                    removeobject(LOCALID_PSYDUCK)
                    addobject(LOCALID_HOOTHOOT)

                    waitstate

                    bufferspeciesname(STR_VAR_1, VAR_0x8009)
                    msgbox(format(
                        "Yeahhh!! I have a {STR_VAR_1} now!\nTake good care of Bread for me, okay?"
                    ), MSGBOX_DEFAULT)
                    closemessage
                }
            }
        }
    }
    release
}

script SakuTown_House3_EventScript_Man {
    lock
    speakername("Man")
    if (flag(FLAG_FORTREE_NPC_TRADE_COMPLETED)) {
        msgbox(format(
            "Oh great, he's got yet another Pokémon…"
        ))
    } else {
        msgbox(format(
            "My son is really into this whole Pokémon trading business, huh…"
        ))
    }
    closemessage
    release
}