const LOCALID_FATGUY = 3
const LOCALID_GUARD = 6

mapscripts SunriseVillage_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        // Random birds on roofs
        setflag(FLAG_TEMP_1)
        setflag(FLAG_TEMP_2)
        setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_4)

        gettime
        if (var(VAR_0x8000) >= 5 && var(VAR_0x8000) <= 17) {
            random(4)
            switch(var(VAR_RESULT)) {
                case 0:
                    clearflag(FLAG_TEMP_1)
                case 1:
                    clearflag(FLAG_TEMP_2)
                case 2:
                    clearflag(FLAG_TEMP_4)
                case 3:
                    clearflag(FLAG_TEMP_1)
                    clearflag(FLAG_TEMP_3)
            }
        }
    }
}

// "Science is amazing" fat guy
script SunriseVillage_Event_FatGuy {
    lock
    faceplayer

    speakername("Neighbour")
    if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 4) {
        msgbox(format(
            "My new Pokémon is so cute, I don't feel like leaving it in its Pokéball!"
        ))
        closemessage
        applymovement(LOCALID_FATGUY, Common_Movement_FaceOriginalDirection)
    }
    else {
        msgbox(format(
            "Someone managed to fit entire Pokémon"
            "inside these tiny balls.\p"
            "The power of science is staggering!"
        ))
        closemessage
    }
    release
    end
}

// Man guarding the forest while you have no Pokémon
script SunriseVillage_Event_GuardForest {
    lock
    applymovement(LOCALID_GUARD, Common_Movement_WalkInPlaceFasterLeft)
    if (flag(FLAG_RECEIVED_STARTER)) {
        speakername("Neighbour")
        if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 8) {
            msgbox(format(
                "Oh, I see you received a Pokémon!\p"
                "You'll probably be able to manage the woods on your own, then.\p"
                "Be careful!"
            ))
            closemessage
        } elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) == 7) {
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
            msgbox(format(
                "Hey, kid, heard you were going off on an adventure!\p"
                "Did you say bye to your sister?\p"
                "No? Thought so. Go see her before you run off, you ungrateful sibling!"
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
            waitmovement(0)
        } else {
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
            msgbox(format(
                "Hey, kid, a friend of yours was looking for you."
            ))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
            waitmovement(0)
        }
    }
    else {
        speakername("Neighbour")
        msgbox(format(
        "Stop right there, kid!"
        ))
        closemessage
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
        speakername("Neighbour")
        msgbox(format(
            "Wild POKéMON have been lashing out recently.\p"
            "I know you're used to walking around on your own, but the woods have become a dangerous place lately!\p"
            "I wouldn't go alone if I were you.\p"
            "Or, you know, if I were me."
        ))
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
        waitmovement(0)
    }
    release
    end
}

script SunriseVillage_Event_GuardForest_Interact {
    lock
    faceplayer
    speakername("Neighbour")
    if (flag(FLAG_RECEIVED_STARTER)) {
        if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 8) {
            msgbox(format(
                "Oh, I see you received a Pokémon!\p"
                "You'll probably be able to manage the woods on your own, then.\p"
                "Be careful!"
            ))
            closemessage
        } elif (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) == 7) {
            msgbox(format(
                "Hey, kid, heard you were going off on an adventure!\p"
                "Did you say bye to your sister?\p"
                "No? Thought so. Go see her before you run off, you ungrateful sibling!"
            ))
            closemessage
        } else {
            msgbox(format(
                "Hey, kid, a friend of yours was looking for you."
            ))
            closemessage
        }
    }
    else {
        msgbox(format(
            "Wild POKéMON have been lashing out recently.\p"
            "Even the Ginko woods have become a dangerous place lately!"
        ))
        closemessage
    }
    release
    end
}

// Little running girl
script SunriseVillage_Event_LittleGirl {
    speakername("Little girl")
    msgbox(format(
        "I can't wait I can't wait I can't wait!!!\p"
        "AHHHHH I CAN'T WAIT!!!!"
    ), MSGBOX_NPC)
}

// Boy
script SunriseVillage_Event_Boy {
    lockall
    faceplayer
    speakername("Kid")
    if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) >= 4) {
        msgbox(format(
            "My Happiny is the strongest of all! See how fierce it looks?"
        ))
        closemessage
    }
    else {
        msgbox(format(
            "Hey {PLAYER}{KUN}!\p"
            "So, d'you think we'll get to choose the one we want?"
        ))
        closemessage
    }
    releaseall
    end
}

// Woman
script SunriseVillage_Event_Woman {
    lockall
    speakername("Neighbour")
    msgbox(format(
        "I can see the Pokéballs from here… I've never seen so many in my life!"
    ))
    releaseall
    end
}

// Farmer
script SunriseVillage_Event_Farmer {
    lockall
    faceplayer
    speakername("Neighbour")
    msgbox(format(
        "I've been waiting for hours!"
    ))
    releaseall
    end
}

// Old man
script SunriseVillage_Event_OldMan {
    lockall
    faceplayer
    speakername("Neighbour")
    msgbox(format(
        "The distribution is about to start."
    ))
    releaseall
    end
}

// Old woman
script SunriseVillage_Event_OldWoman {
    speakername("Neighbour")
    msgbox(format(
        "I'm tired of waiting!"
    ), MSGBOX_NPC)
}

// Happiny
script SunriseVillage_Event_Happiny {
    lock
    faceplayer
    ismonseen(SPECIES_HAPPINY)
    if (var(VAR_RESULT)) {
        speakername("Happiny")
    } else {
        speakername("Noisy egg")
    }
    msgbox(format(
        "Happi!"
    ))
    playmoncry(SPECIES_HAPPINY, CRY_MODE_NORMAL)
    waitmoncry
    release
    end
}

// Munchlax
script SunriseVillage_Event_Munchlax {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_MUNCHLAX, CRY_MODE_NORMAL)
    ismonseen(SPECIES_MUNCHLAX)
    if (var(VAR_RESULT)) {
        speakername("Munchlax")
    } else {
        speakername("Big nom")
    }
    msgbox(format(
        "Munch munch munch…"
    ))
    waitmoncry
    release
    end
}

// Kirlia
script SunriseVillage_Event_Kirlia {
    lock
    faceplayer
    ismonseen(SPECIES_MUNCHLAX)
    if (var(VAR_RESULT)) {
        speakername("Kirlia")
    } else {
        speakername("Posh dancer")
    }
    msgbox(format(
        "…"
    ))
    release
    end
}

// TODO EVA remove
script SunriseVillage_Event_TestGyarados {
    lock
    givemon(SPECIES_SPHEAL, 100)
    setwildbattle(SPECIES_KOTORA, 5)
    dowildbattle
    release
    end
}

script SunriseVillage_Event_TownSign {
    msgbox(format("Sunrise Village\n“A village bathing in soft light.”"), MSGBOX_SIGN)
}

script SunriseVillage_Event_PlayersHouseSign {
    msgbox(format("{PLAYER}'s house"), MSGBOX_SIGN)
}

script SunriseVillage_Event_RivalsHouseSign {
    msgbox(format("{RIVAL}'s house"), MSGBOX_SIGN)
}

script SunriseVillage_Event_EldersHouseSign {
    msgbox(format("Elder Kaba's house"), MSGBOX_SIGN)
}

