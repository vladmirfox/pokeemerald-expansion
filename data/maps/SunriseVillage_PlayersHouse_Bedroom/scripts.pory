mapscripts SunriseVillage_PlayersHouse_Bedroom_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_SUNRISE_VILLAGE_INTRO_STATE) < 1) {
			// setrespawn(HEAL_LOCATION_SUNRISE_VILLAGE)
			setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 1)
		}
	}
}

script SunriseVillage_PlayersHouse_Bedroom_EventScript_Doll {
	lock
	msgbox(format(
		"A very fluffy Wooloo doll. It's made of real Wooloo wool!"
	))
	release
}

script SunriseVillage_PlayersHouse_Bedroom_EventScript_MUGSHOT {
	lock
	setvar(VAR_0x8000, MUGSHOT_AO)
	callnative(DrawMugshot)
	msgbox(format(
		"What up dumdum?"
	))
	closemessage
	callnative(ClearMugshot)
	release
}

// Big Sis calls player when he gets out of bed
script SunriseVillage_PlayersHouse_Bedroom_Event_BigSisCalling {
    lock
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	speakername("Big sis")
	msgbox(format(
		"{PLAYER}! {PLAYER}! Are you still {PK}sleeping?"
	))
	
	dynmultichoice(
		16,
		6,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"Not anymore!",
		"I'm coming!",
		"…"
	)

	closemessage

	speakername("Big sis")

	if (var(VAR_RESULT) == 0) {
		delay(30)
		msgbox(format(
				"Very funny! Now get up and get ready!"
			))
	} else {
		delay(48)

		msgbox(format(
				"… … … … … …\p"
				"…{PLAYER}? Are you up?"
			))

		dynmultichoice(
			16,
			8,
			TRUE,
			10,
			0,
			DYN_MULTICHOICE_CB_NONE,
			"COMIIIIING!!",
			"COMIIIIIIIIIIING!!!"
		)

		closemessage

		delay(30)

		speakername("Big sis")
		msgbox(format(
			"No need to yell, I can hear you just fine!\p"
			"Now get up and get ready!"
		))
	}

	closemessage

	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 2)
	setflag(FLAG_HIDE_SUNRISE_VILLAGE_STANDARD_NPC_POSITIONS)
    release
    end
}