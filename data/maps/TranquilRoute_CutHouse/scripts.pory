const LOCALID_CUTMASTER = 1
const VAR_HOUR_OF_DAY = VAR_0x8000
const FLAG_HIDE_CUT_MASTER = FLAG_TEMP_1

mapscripts TranquilRoute_CutHouse_MapScripts {
	MAP_SCRIPT_ON_LOAD: TranquilRoute_CutHouse_EventScript_CheckCutGrassQuestComplete
	MAP_SCRIPT_ON_TRANSITION {
		if (flag(FLAG_CUT_QUEST_COMPLETED)) {
			gettime
			if (var(VAR_HOUR_OF_DAY) >= 7 && var(VAR_HOUR_OF_DAY) <= 19) {
				setflag(FLAG_HIDE_CUT_MASTER)
			}
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 1: TranquilRoute_CutHouse_EventScript_CutGrassCutscene
    ]
}

script TranquilRoute_CutHouse_EventScript_CheckCutGrassQuestComplete {
	if (flag(FLAG_CUT_QUEST_CUT_ALL_GRASS) && !flag(FLAG_CUT_QUEST_COMPLETED)) {
		setvar(VAR_TEMP_0, 1)
	}
}

script TranquilRoute_CutHouse_EventScript_CutGrassCutscene {
	lock
	setvar(VAR_TEMP_0, 0)
	playse(SE_PIN)
	applymovement(LOCALID_CUTMASTER, TranquilRoute_CutHouse_Movement_CutMasterSurprise)
	waitmovement(0)
	delay(48)

	msgbox(format(
		"You actually cut ALL the tall grass?\p"
		"Wait, let us go outside. I want to see it for myself."
	))
	closemessage

	delay(48)

	warp(MAP_TRANQUIL_ROUTE, 53, 33)
	release
}

movement TranquilRoute_CutHouse_Movement_CutMasterSurprise {
	face_down
	emote_exclamation_mark
	delay_16 * 3
}

script TranquilRoute_CutHouse_EventScript_CutMaster {
	lock
	faceplayer

	// Cut already received
	if(flag(FLAG_RECEIVED_HM_CUT)) {
			// Home for the night
			if(flag(FLAG_CUT_QUEST_CUT_ALL_GRASS)){
				msgbox(format(
					"The sun is setting on another day.\p"
					"Life is good."
				))
				closemessage
			} else {
				// Cut grass quest todo
				msgbox(format(
					"Are you finding the Cut technique useful?\p"
					"No, I still haven't trimmed my garden. I really should get to it…"
				))
				closemessage
			}
	}
	// First time
	else {
		msgbox(format(
			"Hello, there. I don't get many visitors, so sit down, have some tea!\p"
			"What? My garden is overrun by weeds?\p"
			"Ho ho ho…\p"
			"You're right, I have been neglecting it lately. You see, my old back isn't as strong as it used to be.\p"
			"Here, your Pokémon can trim them using this."
		))
		closemessage

		giveitem(ITEM_HM_CUT)
		setflag(FLAG_RECEIVED_HM_CUT)
		msgbox(format(
			"This scroll contains an ancient technique created by the most talented samurai. Pokémon can learn that technique too.\p"
			"If you have a Pokémon capable of learning this move, it will be able to cut tall grass as well as little trees that block your path.\p"
			"In fact, now that you have read the scroll, you don't even need to teach them the actual move!\p"
			"Don't ask me how this works, it just does!\p"
			"Oh, you will need at least one official dojo badge to be able to cut trees and grass, though."
		))
		closemessage
	}

	release
}




script TranquilRoute_CutHouse_EventScript_RandomCryGuy {
	lock
	faceplayer
	if(flag(FLAG_MON_CRY_NPC_GIVE_ITEM))
	{
		msgbox("you already got it right\ncome back tomorrow dingus")
		release
		end
	}

	if(flag(FLAG_MON_CRY_NPC))
	{
		msgbox("you guessed wrong\ncome back tomorrow dingus")
		release
		end
	}

	special(AssignRandomMonCryVars)
	msgbox("yo dingus listen to this")
	waitse
	playmoncry(VAR_0x8008, CRY_MODE_NORMAL)
	waitmoncry
	msgbox("now tell me, whomst was it")

	bufferspeciesname(STR_VAR_1, VAR_0x8004)
	dynmultipush("{STR_VAR_1}", VAR_0x8004)

	bufferspeciesname(STR_VAR_1, VAR_0x8005)
	dynmultipush("{STR_VAR_1}", VAR_0x8005)
	
	bufferspeciesname(STR_VAR_1, VAR_0x8006)
	dynmultipush("{STR_VAR_1}", VAR_0x8006)

	bufferspeciesname(STR_VAR_1, VAR_0x8007)
	dynmultipush("{STR_VAR_1}", VAR_0x8007)

	dynmultistack(0, 0, FALSE, 6, TRUE, 3, DYN_MULTICHOICE_CB_NONE)

    if (var(VAR_RESULT) == VAR_0x8008) 
	{
		msgbox("correct dingus, here take this")
		giveitem(ITEM_MEWTWONITE_X)
		setflag(FLAG_MON_CRY_NPC_GIVE_ITEM)
		msgbox("come back tomorrow")
	}
	else
	{
		msgbox("wrong dingus, try again tomorrow")
	}
	setflag(FLAG_MON_CRY_NPC)
	release
	end
}



script TranquilRoute_CutHouse_EventScript_RandomDexGuy {
	lock
	faceplayer
	msgbox("alright dingus, ima give you a dex\ndescription, you give me the mon\pand remember, you only get to see\nthis once because im lazy")
	closemessage
	randomdexmsgbox
	msgbox("now whomst was that about")
	special(EnterDexRiddleGuess)
	waitstate
	special(GetDexRiddleFeedback)
	if(var(VAR_RESULT) == 1)
	{
		msgbox("holy shit you got it right")
	}
	else
	{
		msgbox("wrong as usual dingus")
	}
	release
	end
}
