raw `
MauvilleCity_PokemonCenter_2F_MapScripts::
	map_script MAP_SCRIPT_ON_FRAME_TABLE, CableClub_OnFrame
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, CableClub_OnWarp
	map_script MAP_SCRIPT_ON_LOAD, CableClub_OnLoad
	map_script MAP_SCRIPT_ON_TRANSITION, CableClub_OnTransition
	.byte 0

@ The below 3 are unused and leftover from RS
MauvilleCity_PokemonCenter_2F_EventScript_Colosseum::
	call CableClub_EventScript_Colosseum
	end

MauvilleCity_PokemonCenter_2F_EventScript_TradeCenter::
	call CableClub_EventScript_TradeCenter
	end

MauvilleCity_PokemonCenter_2F_EventScript_RecordCorner::
	call CableClub_EventScript_RecordCorner
	end

MauvilleCity_PokemonCenter_2F_EventScript_Youngster::
	msgbox MauvilleCity_PokemonCenter_2F_Text_Youngster, MSGBOX_NPC
	end

MauvilleCity_PokemonCenter_2F_Text_Youngster:
	.string "Did you know that you can link battle\n"
	.string "at the COLOSSEUM here?\p"
	.string "They put up your record on the wall\n"
	.string "for everyone to see.\p"
	.string "It's embarrassing if you lose more\n"
	.string "often than you winâ€¦$"

`

script Custom_Givemon_Mauville{

    lockall
    showmonpic(SPECIES_LARVITAR, 10,3)
    msgbox("Do you want this Larvitar?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        givemon(SPECIES_LARVITAR, 10, ITEM_LUXURY_BALL)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Here you go!")
            removeobject(6)
            setflag(FLAG_UNUSED_0x022)
            call(MauvilleNicknamePartyMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Larvitar was placed in your PC.")
            removeobject(6)
            setflag(FLAG_UNUSED_0x022)
            call(MauvilleNicknameBoxMon)
        }
        else{
            hidemonpic
            msgbox("You need to release something to get this.")
        }
    }
    else{
        hidemonpic
        msgbox(Custom_Givemon_Mauville_NotChosen)
    }
    waitmessage
    releaseall
    end

}

script MauvilleNicknamePartyMon{

    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }


}

script MauvilleNicknameBoxMon{

    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    call(Common_EventScript_TransferredToPC)

}

text Custom_Givemon_Mauville_NotChosen{
    "Guess I'll just have to use it for spells..."
}
