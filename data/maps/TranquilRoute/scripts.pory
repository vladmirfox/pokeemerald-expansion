const LOCALID_BERRYWOMAN = 5
const LOCALID_MYSTERIOUS_SAMURAI = 9


mapscripts TranquilRoute_MapScripts {

}

script TranquilRoute_EventScript_Chikao {
	trainerbattle_single(
		TRAINER_CHIKAO,
		format("I've been studying Pokémon at school! I can't lose!"),
		format("I should have listened in class…")
	)
	msgbox("Some Pokémon have more attack, and some have more special attack!\p What's the difference? Uhh… It's too complicated to explain.", MSGBOX_AUTOCLOSE)
	end
}

script TranquilRoute_EventScript_Takeshi {
	trainerbattle_single(
		TRAINER_TAKESHI,
		format("Stay right there! I'll defeat you with my ninja skills!"),
		format("I had the sun in my eyes!")
	)
	msgbox(format("I must hone my ninja skills!"), MSGBOX_AUTOCLOSE)
	end
}

script TranquilRoute_EventScript_Hana {
	trainerbattle_single(
		TRAINER_HIINA,
		format("Ugh, how can Kenji paint a portrait of me if people keep walking between us?"),
		format("Great, now my hair is all messed up.")
	)
	msgbox(format("Kenji really is an amazing artist. I feel very lucky."), MSGBOX_AUTOCLOSE)
	end
}

script TranquilRoute_EventScript_Kenji {
	trainerbattle_single(
		TRAINER_KENJI,
		format("You're interrupting my creative flow!!"),
		format("Such beauty! What an artistic fight this was!")
	)
	msgbox(format(
		"Hiina is the best subject I could dream of.\p"
		"…Hey, uh, please don't tell her I said that."
	), MSGBOX_AUTOCLOSE)
	end
}

// Evil team member
script TranquilRoute_EventScript_MysteriousSamurai {
	lock
	// TODO EVA
	msgbox(format(
		"So you are one of those new Pokémon wielders, right?\p"
		"You say Pokémon have been restless, lately?\p"
		"Interesting.\p"
		"I have the feeling we're going to meet again soon.\p"
		"Until then, farewell."
	))
	closemessage

	applymovement(LOCALID_MYSTERIOUS_SAMURAI, TranquilRoute_Movement_MysteriousSamuraiWalksAway)
	removeobject(LOCALID_MYSTERIOUS_SAMURAI)

	setflag(FLAG_MYSTERIOUS_SAMURAI_TRANQUIL_ROUTE)
	release
	end
}

movement TranquilRoute_Movement_MysteriousSamuraiWalksAway {
	walk_right * 6
}

script TranquilRoute_EventScript_Girl {
	lock
	msgbox(format("What? Oh, I don't fight.\pMy Pokémon and I are just strolling."), MSGBOX_AUTOCLOSE)
	release
	end
}

script TranquilRoute_EventScript_Shellos {
	lock
	faceplayer
	playmoncry(SPECIES_SHELLOS, CRY_MODE_NORMAL)
	msgbox(format(
		"Clelel!"
	))
	waitmoncry
	closemessage
	release
	end
}

script TranquilRoute_EventScript_BerryWoman {
	lock
	faceplayer
	// TODO EVA add speech bubble with  little musical notes
	if (flag(FLAG_RECEIVED_WAILMER_PAIL)) {
		msgbox(format("Enjoy gardening!"))
	}
	else {
		msgbox(format("Hi, young one! See this?"))
		closemessage
		applymovement(LOCALID_BERRYWOMAN, Common_Movement_FaceRight)
		waitmovement(0)
		delay(48)
		applymovement(LOCALID_BERRYWOMAN, Common_Movement_FacePlayer)
		waitmovement(0)
		msgbox(format(
			"You can plant berries in the ground in fertile spots like this.\p"
			"The seeds will eventually grow into a healthy berry tree, ready to be harvested!\p"
			"I see you're a wielder. Well, different berries have different effects when they're fed to Pokémon.\p"
			"I love berries, hopefully you'll soon learn to love them too!\p"
			"Here, try planting a few!"
		))
		closemessage
		giveitem(ITEM_ORAN_BERRY, 3)
		goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)

		msgbox(format(
			"Oh, right, I was almost forgetting!\p"
			"You must remember to water them regularly, or they won't grow!\p"
			"I like you. Take this!"
		))
		closemessage
		giveitem(ITEM_WAILMER_PAIL)
		goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)

		msgbox(format(
			"Isn't it cute? It's friend-shaped!\p"
			"Don't worry about it, I have a spare one.\p"
			"Have fun gardening!"
		))

		setflag(FLAG_RECEIVED_WAILMER_PAIL)
	}
	release
	end
}

script TranquilRoute_EventScript_Mareep {
	lock
	playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
	msgbox(format("Beeeeeh…"), MSGBOX_AUTOCLOSE)
	waitmoncry
	release
}

script TranquilRoute_EventScript_Mareep2 {
	lock
	playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
	msgbox(format("Baaaaah…"), MSGBOX_AUTOCLOSE)
	waitmoncry
	release
}

script TranquilRoute_TrainerTips1 {
	msgbox(format(
		"Did you know?\p"
		"Some Pokémon only come out at night.\p"
		"Visit the same spot at different times of day to have a chance of seeing rare Pokémon!"
	), MSGBOX_SIGN)
}


script TranquilRoute_TrainerTips2 {
	msgbox(format(
		"Did you know?\p"
		"Several items can be assigned to the SELECT button from your bag."
	), MSGBOX_SIGN)
}

script TranquilRoute_DirectionSign {
	if (flag(FLAG_SAKU_KURA_REUNITED)) {
		msgbox(format(
			"{LEFT_ARROW} Whiteslate route\n"
			"{UP_ARROW} Sakura Village\p"
			"It looks like the sign has been written over recently…"
		), MSGBOX_SIGN)
	} else {
		msgbox(format(
			"{LEFT_ARROW} Whiteslate route\n"
			"{UP_ARROW} Saku and Kura Villages"
		), MSGBOX_SIGN)
	}
}
