const LOCALID_GUARD = 2

mapscripts KuraTown_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		// VAR_SAKU_KURA_VILLAGES_STATE, 1: KuraTown_EventScript_GuardStopsYou
	]
	MAP_SCRIPT_ON_LOAD {
		// Barricading dojo
		if (flag(FLAG_KURA_DOJO_CLOSED)) {
			setmetatile(40, 16, METATILE_SakuKura_ClosedDojoDoor, TRUE)
		}
	}
}

script KuraTown_EventScript_GuardStopsYou {
	lock

	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	// Player sneaks in from the left
	if (var(VAR_TEMP_0) <= 14) {
		applymovement(LOCALID_GUARD, Common_Movement_WalkInPlaceFasterLeft)
		waitmovement(0)
		msgbox(format("Hey!"))
		closemessage
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
		waitmovement(OBJ_EVENT_ID_PLAYER)
		// Player sneaks in from the right
	} else {
		applymovement(LOCALID_GUARD, Common_Movement_WalkInPlaceFasterRight)
		waitmovement(0)
		msgbox(format("Hey!"))
		closemessage
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
		waitmovement(OBJ_EVENT_ID_PLAYER)
	}
	delay(20)

	if (var(VAR_SAKU_KURA_VILLAGES_STATE) == 1) {
		msgbox(format(
			"You think I don't see you trying to sneak in from Saku Village?\p"
			"…Wait, you're a regular traveler, aren't you…\p"
			"Welcome to Kura Village then!"
		))
		closemessage
		setvar(VAR_SAKU_KURA_VILLAGES_STATE, 2)
	} elif (var(VAR_SAKU_KURA_VILLAGES_STATE) == 2) {
		msgbox(format(
			"You're not trying to sneak into Saku Village, are you?\p"
			"…My bad, you're that traveler again. Didn't recognize ya.\p"
			"You can go through, though I'm not sure why you'd want to."
		))
		closemessage
		setvar(VAR_SAKU_KURA_VILLAGES_STATE, 3)
	}
	release
}

script KuraTown_EventScript_DojoChallenger {
	lock
	faceplayer
	if (flag(FLAG_KURA_DOJO_CLOSED)) {
		msgbox(
			format(
				"Looks like the dojo is closed again…\p"
				"Gah, when will I get a chance to crush Hana in a fiery battle?\p"
				"Could it be that she is scared of me?"
			)
		)
	} else {
		msgbox(
			format(
				"The dojo is open! I can finally defeat its leader in a fiery battle!\p"
				"…Hm? When, you ask?\p"
				"In… In a minute…\p"
				"No, go right ahead, I'm just… preparing…"
			)
		)
	}
	closemessage
	release
}

script KuraTown_EventScript_Farmer {
	lock
	faceplayer
	speakername("Farmer")
	msgbox(
		format(
			"Why do we hate the Saku Village folks, y'ask?\p"
			"To be honest, I don't quite rememb'r. But who needs a reas'n, right?"
		),
		MSGBOX_DEFAULT
	)
	release
}

script KuraTown_EventScript_FarmerScyther {
	lock
	faceplayer
	speakername("Farmer")
	msgbox(
		format(
			"Our Scyther harvest the crops once they've grown."
		),
		MSGBOX_DEFAULT
	)
	release
}

script KuraTown_EventScript_Scyther {
	lock
	faceplayer
	playmoncry(SPECIES_SCYTHER, CRY_MODE_NORMAL)
	speakername("Scyther")
	msgbox(format("Gyooo!"))
	waitmoncry
	closemessage
	release
}

script KuraTown_EventScript_Guard {
	lock
	faceplayer
	msgbox(
		format(
			"Hope you didn't have too much trouble passing the gate.\p"
			"That Saku Village guard is such a jerk, he won't let anyone from our village cross to theirs.\p"
			"So of course we're not gonna let anyone from theirs in ours! Hah!"
		),
		MSGBOX_DEFAULT
	)
	release
}

script SakuTown_EventScript_ClosedGymDoor {
	// Barricading dojo
	if (flag(FLAG_KURA_DOJO_CLOSED)) {
		msgbox(format(
			"“BE BACK SOON\n"
			"- Hana”"
		), MSGBOX_SIGN)
	}
}

script KuraTown_EventScript_TownSign {
	msgbox(format("Kura Village\n“A village where the mountains meet the woods.”"), MSGBOX_SIGN)
}

script KuraTown_EventScript_TrainerTips {
	msgbox(format("Did you know?\pThe people from Saku Village can and will eat your children!"), MSGBOX_SIGN)
}

script KuraTown_EventScript_DirectionsSign {
	msgbox(format("{UP_ARROW} Silver Tunnel\nThis way for Silveridge"), MSGBOX_SIGN)
}

script KuraTown_EventScript_GymSign {
	msgbox(
		format(
			"Kura Village Pokémon Dojo\n"
			"Leader: Hana\p"
			"“Strong as a maple rooted in wisdom.”"
		),
		MSGBOX_SIGN
	)
}

